!-----------------------------------------------------------------------------
! (C) Crown copyright 2022 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> @brief Algorithm to update age-of-air tracer field
module ageofair_alg_mod

  use clock_mod,                      only: clock_type
  use constants_mod,                  only: r_def
  use field_mod,                      only: field_type
  use io_config_mod,                  only: subroutine_timers
  use timer_mod,                      only: timer
  use ageofair_kernel_mod,            only: ageofair_kernel_type
  use transport_config_mod,           only: ageofair_reset_level

  implicit none

  private

  public  :: init_ageofair
  public  :: ageofair_update

contains

  !===========================================================================!
  !> @details This routine initialises the age-of-air field.
  !> @param[in,out] ageofair   age-of-air field
  subroutine init_ageofair(ageofair)

    implicit none

    type(field_type), intent(inout) :: ageofair

    call invoke( setval_c( ageofair, 0.0_r_def ) )

  end subroutine init_ageofair

  !===========================================================================!
  !> @details Algorithm to update age-of-air tracer field
  !> @param[in,out]     ageofair            Age-of-air tracer field
  !> @param[in]         clock               The model clock
  subroutine ageofair_update( ageofair, clock )

    implicit none

    type(field_type), intent(inout) :: ageofair
    class(clock_type), intent(in)   :: clock

    type(field_type) :: ones
    real(r_def)      :: dt

    if ( subroutine_timers ) call timer( 'ageofair_advection_alg' )

    dt = real(clock%get_seconds_per_step(), r_def)

    ! Add dt to all levels of ageofair
    call ones%initialise( vector_space = ageofair%get_function_space() )
    call invoke( setval_c(ones, 1.0_r_def),       &
                 inc_X_plus_bY(ageofair,dt,ones), &
                 ! Set levels nearest the surface to be zero in ageofair field
                 ageofair_kernel_type(ageofair,ageofair_reset_level) )

    if ( subroutine_timers ) call timer( 'ageofair_advection_alg' )

  end subroutine ageofair_update

end module ageofair_alg_mod
